//===== rAthena Script ======================================= 
//= Renewal Mage Job Quest
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Job Change to Mage Class
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//= 1.1 Added support for Baby classes. [Euphy]
//=============== Tradução PT-BR ================
// HanzoBR - Disponibilizado para Projeto SOCERO
//===============================================
// revisado por Haikenz

geffen_in,164,124,4	script	Mago#mage	123,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Wizard || ADVJOB == Job_Professor) {
			if (Class == Job_Novice_High) {
				mes "[Mago]";
				mes "Uau, muito tempo que não te vejo, não deveria estar morto?";
				next;
				mes "[Mago]";
				mes "Ah sim, você deve ter renascido. Estou feliz que voltou.";
				next;
				if (!callfunc("F_CanChangeJob")) {
					mes "[Mago]";
					mes "Desculpe, você não está pronto para aprender magia. Você nem aprendeu as habilidades básicas ainda.";
					next;
					mes "[Mago]";
					mes "Sem pressa. Quando mais você aprende, mais preparado para a magia você ficará.";
					close;
				}
				mes "[Mago]";
				mes "Bom, desde que você passou no teste de mago uma vez não irei questiona-lo. Quer ter suas Magias de volta não é mesmo?";
				next;
				skill "NV_TRICKDEAD",0,SKILL_PERM;
				jobchange Job_Mage_High;
				skill "MG_ENERGYCOAT",1,SKILL_PERM;
				mes "[Mago]";
				mes "Uau! Por algum motivo você parece estar muito melhor do que antes. Acredito que fará um ótimo trabalho como Mago também.";
				close;
			}else{
				mes "[Mago]";
				mes "Posso ajudar em algo mais? Se não, vá, corra, teste suas habilidades! O Mundo espera por você.";
				close;
			}
		}else{
			mes "[Mago]";
			mes "O que ? Você está interessado na Guilda dos Magos? Eu não queria dizer isto mas, você não pertence aqui.";
			next;
			mes "[Mago]";
			mes "Não sei porque ainda está de pé na minha frente. Mas posso dizer que sei que você não é um Mago.";
			close;
		}
	}
	mes "[Mago]";
	if(BaseJob != Job_Novice) {
		if(BaseJob == Job_Mage) {
			mes "Você não percebeu? Você já é um Mago, tonto.";
			mes "Um dia irá sentir o poder dentro de você que poderá fazer fogo com sua mente.";
		} else
			mes "Qual é!? Você não pode ser um Mago, já tem sua classe!";
		close;
	}else{
		mes "Como posso ajudar?";
		next;
		switch(select("Quero me tornar Mago:O que preciso para me tornar Mago?:Nada, obrigado.")) {
		case 1:
			mes "[Mago]";
			mes "Quer se tornar Mago é ?";
			next;
			mes "[Mago]";
			if (Sex == SEX_MALE) {
				mes "Olhe para você. Não faz meu tipo, é muito fofo.";
			}else{
				mes "Ahhhh *-*";
				mes "Você é tão linda, eu gosto de garotas como você.";
			}
			mes "Certo, você disse que quer se tornar Mago né?";
			next;
			if(select("Sim, quero me tornar Mago.:Nada disse nada.") == 2) {
				mes "[Mago]";
				mes "O que~? Está de brincadeira comigo? Primeiro diz que quer se tornar Mago, depois muda de ideia? Se decide ! Não estou de brincadeira.";
				close;
			}
			mes "[Mago]";
			if (!callfunc("F_CanChangeJob")) {
				mes "Suas habilidades básicas não atingem o nível requerido.";
				mes "Volte e upe essa habilidade garoto";
				close;
			}
			mes "Ok, tudo certo... Assine o formulário de inscrição. Nossa! Que caligrafia incrível!";
			mes "Você é... "+strcharinfo(0)+".";
			next;
			mes "[Mago]";
			mes "Hmm, podemos ver que você deu seu jeito, meio desajeitado mas acho que está tudo bem.";
			mes "Escolhendo o básico, sempre melhor opção. Irei transforma-lo agora!";
			next;
			mes "[Mago]";
			mes "Haa! Você agora é um Mago, um de nós";
			mes "Bem vindo a Guilda dos Magos, nosso novo amigo!";
			next;
			mes "[Mago]";
			mes "'Bem vindo a Guilda dos Magos'";
			mes "Parabéns por se tornar um membro da Guilda dos Magos. É isso ai!";
			callfunc "Job_Change",Job_Mage;
			getitem 1639,1;	// N_Rod
			close;
		case 2:
			mes "[Mago]";
			mes "Quer ser um mago?";
			mes "Eu ficaria feliz em explicar os requisitos para uma garota linda que nem você.";
			next;
			mes "[Mago]";
			mes "Primeiro de tudo, deve ser Aprendiz, alcançar nível 10 de Base e aprender todas habilidades básicas.";
			next;
			mes "[Mago]";
			mes "No passado havia um teste com poções, por causa disto, perdemos um candidato, lentamente.";
			mes "Então acabamos decidindo aceitar todos que conseguem concluir os requisitos mínimos.";
			next;
			mes "[Mago]";
			mes "Não hesite! Torne-se um de nós! Vire um poderoso Mago!!";
			close;
		case 3:
			mes "[Mago]";
			mes "Nada mesmo...?";
			close;
		}
	}
}