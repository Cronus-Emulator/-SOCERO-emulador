//===== rAthena Script ======================================= 
//= Soul Linker Job Quest
//===== By: ================================================== 
//= Celestria & Samuray22
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Job quest for Soul Linker class.
//= [Vars Used]
//= SOUL_Q = Job Change Phase. (Max 4)
//===== Additional Comments: ================================= 
//= 1.2 Rescripted to Aegis 10.3 standards. [Samuray22]
//= 1.3 Slight updates and fixes to the script. [L0ne_W0lf]
//=     Removes the use of labels, and corrects other errors.
//=     Removed the GM-use only NPC from the sec_in map. 
//=     No longer used a global var, uses an NPC var instead.
//= 1.3a Deleted Empty Color Tag. (bugreport:1572) [Samuray22]
//= 1.4 Replaced effect numerics with constants. [L0ne_W0lf]
//= 1.5 Added Quest Log commands. [Kisuka]
//= 1.6 Updated to match the latest official script. [Euphy]
//= 1.7 Tradução PT-BR [Clare]
//= 1.7.1 Adaptação para rAthena [HD Scripts]
//============================================================ 

morocc_in,174,30,6	script	Garoto#link1	716,{
	if (Class == Job_Soul_Linker) {
		mes "[Maia]";
		mes "Boa sorte em sua";
		mes "jornada. Quando você";
		mes "dominar mais habilidades de Espiritualista,";
		mes "você será capaz de usar mais";
		mes "poderes dos Espíritos sobre";
		mes "os seus companheiros...";
		close;
	}
	if (Class == Job_Star_Gladiator) {
		mes "[Garoto]";
		if (Sex == SEX_MALE) {
			mes "Você é um guerreiro";
			mes "do sol? Eu estou familiarizado";
		}
		else {
			mes "Você é um guerreiro";
			mes "da lua? Eu estou familiarizado";
		}
		mes "com seus caminhos. Afinal,";
		mes "a base de nossas habilidades";
		mes "estão fundamentadas nas da";
		mes "classe Taekwon, certo?";
		close;
	}
	if (Class != Job_Taekwon) {
		mes "[Garoto]";
		mes "Hm... Eu não tenho nada a";
		mes "oferecer para você. Mas se você conhecer";
		mes "algum praticante de Taekwon Dô";
		mes "experiente, mande-os até mim,";
		mes "pelo que sei eles poderão se";
		mes "beneficiar com meus conhecimentos.";
		close;
	}
	if (JobLevel < 40) {
		mes "[Garoto]";
		mes "Então você está estudando";
		mes "Taekwon Dô. Isso é bom,";
		mes "na verdade muito bom. Basta";
		mes "aprimorar sua habilidades e";
		mes "manter um bom treinamento.";
		close;
	}
	else if (JobLevel > 39) {
		if (SOUL_Q == 0) {
			mes "[Garoto]";
			mes "...";
			mes "Ei você.";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "Você está me chamando?";
			next;
			mes "[Garoto]";
			mes "Sim, eu chamei você.";
			mes "Agora não me faça";
			mes "falar muito alto, e";
			mes "venha até aqui.";
			next;
			if (select("Você está sendo muito rude para um garoto!:Ignorá-lo.") == 1) {
				mes "[Garoto]";
				mes "Você está com sorte,";
				mes "tenho algo interessante";
				mes "para você! Você pode me ver";
				mes "como uma criança, Mas eu tenho";
				mes "mais de 300 anos de idade!";
				emotion ET_HNG;
				next;
				mes "[Garoto]";
				mes "Agora ouça...";
				mes "Eu sei que você é um";
				mes "discípulo de Taekwon Dô.";
				mes "É uma arte respeitável, mas";
				mes "eu gostaria de lhe fazer";
				mes "uma proposta.";
				emotion ET_SMILE;
				next;
				mes "[Garoto]";
				mes "Eu estive olhando para você, e eu";
				mes "posso dizer que você tem um grande";
				mes "poder espiritual. E esse";
				mes "seu potêncial não deve ser desperdiçado.";
				mes "Então porque não fazer de";
				mes "você um ''Espiritualista''?";
				next;
				if (select("Ha! garoto bobo~:Espiritualista?") == 1) {
					mes "[Garoto]";
					mes "Você... você não";
					mes "acredita em mim? Eu estou";
					mes "falando sério. Você pode";
					mes "pelo menos por um minuto";
					mes "esquecer o fato de que eu pareço";
					mes "uma criança? *Psh* ...Jovens.";
					close;
				}
				mes "[Garoto]";
				mes "Espiritualistas são capazes de";
				mes "comunicar-se com Espíritos de";
				mes "antigos guerreiros mortos, que ainda";
				mes "desejam lutar no mundo dos vivos. Agora, esses";
				mes "Espíritos guerreiros não podem";
				mes "lutar livremente em nosso mundo.";
				next;
				mes "[Garoto]";
				mes "No entanto, uma vez que";
				mes "que eles são invocados, esses Espíritos";
				mes "são atraídos para você.";
				mes "Com o treinamento correto, você pode";
				mes "temporariamente atribuir o poder desses Espíritos aos seus aliados.";
				next;
				mes "[Garoto]";
				mes "Agora, você pode atribuir também";
				mes "o poder desses Espíritos a si mesmo. Dependendo,";
				mes "de suas habilidades de Espiritualista,";
				mes "você poderá apenas atribuir";
				mes "o poder dos Espíritos para algumas classes.";
				next;
				mes "[Garoto]";
				mes "Você vai ter de entrar em um mundo";
				mes "totalmente diferente";
				mes "para se tornar um Espiritualista,";
				mes "mas eu sei que isso vai ser possível";
				mes "para você. E então o que me diz?";
				next;
				if (select("Não. pelo menos, não agora...:Tudo bem. O que eu tenho de fazer?") == 1) {
					mes "[Garoto]";
					mes "Ah, Tudo bem. Mas,";
					mes "se você decidir";
					mes "tornar-se um Espiritualista,";
					mes "volte até aqui";
					mes "e fale comigo a qualquer momento.";
					close;
				}
				set SOUL_Q,1;
				setquest 6005;
				mes "[Garoto]";
				mes "Então você está decidido";
				mes "a tornar-se um Espiritualista? Muito bom!";
				mes "Tudo bem, primeiro você precisa";
				mes "me trazer alguns itens.";
				mes "Não se preocupe, eu vou explicar";
				mes "por que você precisa deles, mais tarde.";
				next;
				mes "[Garoto]";
				mes "Agora traga-me";
				mes "^0000FF1 Diamante de 3 Quilates^000000,";
				mes "^0000FF1 Coração Imortal^000000 e";
				mes "^0000FF1 Rosa Eterna^000000.";
				mes "E não me faça esperar";
				mes "por muito tempo, ok?";
				close;
			}
			mes "[Garoto]";
			mes "Huh...?";
			mes "Espere, para onde";
			mes "você vai? Eu...";
			mes "Eu estou falando com você!";
			close;
		}
		else if (SOUL_Q == 1) {
			if (Class == Job_Taekwon) {
				mes "[Garoto]";
				mes "Você voltou, em?";
				mes "E então você trouxe";
				mes "^0000FF1 Diamante de 3 Quilates^000000,";
				mes "^0000FF1 Coração Imortal^000000 e";
				mes "^0000FF1 Rosa Eterna^000000.";
				mes "assim como eu pedi?";
				next;
				if (select("Sim, eu trouxe.:Não, ainda não...") == 1) {
					if (countitem(732) > 0 && countitem(929) > 0 && countitem(748) > 0) {
						delitem 732,1; // Diamante de 3 Quilates
						delitem 929,1; // Coração Imortal
						delitem 748,1; // Rosa Eterna
						set SOUL_Q,2;
						changequest 6005,6006;
						mes "[Garoto]";
						mes "Ótimo, eu vejo que você";
						mes "me trouxe tudo. Mas";
						mes "antes de começar, deixe";
						mes "que eu me apresente. Meu nome";
						mes "é Maia, e eu estou vivo por mais de 300 anos.";
						next;
						mes "[Maia]";
						mes "Bem mas pulando os";
						mes "detalhes, é o meu dever encontrar e";
						mes "recrutar mais Espiritualistas. Essa é a razão";
						mes "pela qual eu não deixei, você sabe, passar direto.";
						next;
						mes "[Maia]";
						mes "De toda forma, eu preciso terminar";
						mes "os preparativos com os materiais";
						mes "que você me trouxe, então preciso";
						mes "que você saia e volte depois, ok?";
						mes "E então, nos falaremos novamente.";
						close;
					}
					mes "[Garoto]";
					mes "Hm...?";
					mes "Ei, você esqueceu";
					mes "de alguma coisa. Agora";
					mes "vá e me traga tudo";
					mes "que eu havia";
					mes "te pedido, ok?";
					emotion ET_HNG;
					next;
					mes "[Garoto]";
					mes "Eu sei que você sabe o que";
					mes "precisa trazer, mas eu vou";
					mes "repetir novamente:";
					mes "^0000FF1 Diamanete de 3 Quilates^000000,";
					mes "^0000FF1 Coração Imortal^000000 e";
					mes "^0000FF1 Rosa Eterna^000000.";
					close;
				}
				mes "[Garoto]";
				mes "Hm. Mas isso é bom.";
				mes "Embora eu tenha todo";
				mes "tempo do mundo,";
				mes "eu não gosto";
				mes "de esperar muito.";
				close;
			}
			set SOUL_Q,0;
			mes "[Garoto]";
			mes "Você se tornou um guerreiro";
			mes "do Sol, da Lua e";
			mes "das Estrelas em? Eu não tinha";
			mes "ideia de que você tinha todo esse potencial.";
			mes "Acho que não posso culpá-lo...";
			close;
		}
		else if (SOUL_Q == 2) {
			if (SkillPoint) {
				mes "[Maia]";
				mes "Você ainda possui";
				mes "alguns pontos não distribuidos.";
				mes "Use todos eles para aprender";
				mes "as habilidades de Taekwon Dô, e depois";
				mes "volte quando estiver pronto.";
				close;
			}
			if (.SoulLinkerTest == 1) {
				mes "[Maia]";
				mes "Certo agora, preciso";
				mes "terminar a cerimônia";
				mes "que vai transformá-lo em Espiritualista.";
				mes "Então você poderia esperar";
				mes "até que eu termine? Então, quando estiver";
				mes "pronto, eu irei atendê-lo.";
				close;
			}
			donpcevent "Timer#link3::OnEnable";
			set .SoulLinkerTest,1;
			mes "[Maia]";
			mes "Ótimo, eu terminei";
			mes "os preparativos. Agora";
			mes "está pronta a cerimônia";
			mes "de mudança para você";
			mes "tornar-se um Espiritualista.";
			mes "Agora feche seus olhos...";
			close2;
			warp "job_soul",30,30;
			end;
		}
		else if (SOUL_Q > 2) {
			mes "[Maia]";
			mes "Você está pronto";
			mes "para entrar nas profudenzas";
			mes "da sua mente de novo?";
			next;
			if (select("Não:Sim") == 1) {
				mes "[Maia]";
				mes "Então, volte";
				mes "a falar comigo quando";
				mes "você estiver pronto.";
				mes "Até lá, eu estarei";
				mes "esperando bem aqui.";
				close;
			}
			if (.SoulLinkerTest == 1) {
				mes "[Maia]";
				mes "Certo agora, preciso";
				mes "terminar a cerimônia";
				mes "que vai transformá-lo em Espiritualista.";
				mes "Então você poderia esperar";
				mes "até que eu termine? Então, quando estiver";
				mes "pronto, eu irei atendê-lo.";
				close;
			}
			donpcevent "Timer#link3::OnEnable";
			set .SoulLinkerTest,1;
			mes "[Maia]";
			mes "Tudo bem, feche";
			mes "seus olhos e relaxe.";
			mes "Você será levado para as";
			mes "profundezas de sua mente.";
			close2;
			warp "job_soul",30,30;
			end;
		}
	}

OnInit:
	set .SoulLinkerTest,0;
	end;
}

job_soul,30,31,0	script	Maia#link2::SLTester	-1,3,3,{
OnTouch:
	if (Class == Job_Taekwon) {
		if (JobLevel < 40) {
			set getvariableofnpc(.SoulLinkerTest,"Garoto#link1"),0;
			mes "[Maia]";
			mes "Hm? Como você veio";
			mes "parar aqui? Você não está qualificado";
			mes "para esta cerimônia. Vou levá-lo de volta para Morroc...";
			close2;
			warp "morocc",157,47;
			end;
		}
		if (SOUL_Q == 2) {
			mes "[Maia]";
			mes "Você reconhece esse";
			mes "lugar? Certo, agora estamos";
			mes "dentro de sua mente. Os Espíritos";
			mes "dos guerreiros que morreram";
			mes "estão aqui, esperando que você";
			mes "invoque seus poderes.";
			next;
			mes "[Maia]";
			mes "Certo, agora há apenas";
			mes "alguns deles aqui, mas se";
			mes "você continuar treinando, você";
			mes "será capaz de invocar mais";
			mes "Espíritos como um Espiritualista.";
			next;
			set SOUL_Q,3;
			changequest 6006,6007;
			mes "[Maia]";
			mes "Nós só podemos ficar aqui";
			mes "na sua mente por 3 minutos.";
			mes "Eu sugiro que você fale com";
			mes "os Espíritos enquanto";
			mes "tem a oportunidade.";
			close;
		}
		else if (SOUL_Q == 3) {
			mes "[Maia]";
			mes "Ouça o que";
			mes "os Espíritos estão tentando te dizer.";
			mes "Há uma razão pela qual";
			mes "eles não podem seguir em frente";
			mes "para o próximo mundo.";
			close;
		}
		else if (SOUL_Q == 4) {
			mes "[Maia]";
			mes "Eu acredito que você";
			mes "já está pronto para tornar-se";
			mes "um Espiritualista. No entanto,";
			mes "você pode continuar";
			mes "a falar com os Espíritos";
			mes "se for isso que você deseja.";
			next;
			if (select("Conversar mais com os espíritos:Tornar-se um Espiritualista") == 1) {
				mes "[Maia]";
				mes "Tudo bem. Mas tente ser";
				mes "rápido, uma vez que há";
				mes "um limite de tempo para permanecer";
				mes "em sua mente. Embora, nós possamos";
				mes "voltar à sua mente se";
				mes "você falar comigo mais tarde...";
				close;
			}
			if (ismounting()) {
				mes "[Maia]";
				mes "Você está em uma montaria,";
				mes "então você não pode mudar de classe.";
				mes "Por favor, remova sua montaria e tente novamente!";
				close;
			}
			mes "[Maia]";
			mes "Então vamos começar a";
			mes "cerimônia. Esses itens serão";
			mes "usados para você ganhar a capacidade";
			mes "de conjurar os poderes dos";
			mes "Espíritos antigos em seus companheiros";
			mes "no campo de batalha.";
			next;
			mes "[Maia]";
			mes "Esta rosa eterna vai morrer";
			mes "ao invés de você...";
			specialeffect EF_MAPPILLAR2,AREA,"Maia#link2";
			next;
			mes "[Maia]";
			mes "Esta rosa eterna vai morrer";
			mes "ao invés de você...";
			mes "Este coração imortal irá";
			mes "bombear o sangue ao invés do seu. ";
			next;
			mes "[Maia]";
			mes "Esta rosa eterna vai morrer";
			mes "ao invés de você...";
			mes "Este coração imortal irá";
			mes "bombear o sangue ao invés do seu. Este diamante irá se tornar pó,";
			mes "ao invés do seu corpo mortal.";
			next;
			mes "[Maia]";
			mes "Os mortos que desejarem";
			mes "continuarão a lutar...";
			mes "Lutarão com você! Use seus poderes";
			mes "de Espiritualista";
			mes "com sabedoria e para fins justos.";
			next;
			if (SkillPoint) {
				mes "^0000ffVocê ainda tem pontos de habilidade sobrando. Por favor, use todos os pontos de habilidades restantes e tente novamente!^000000";
				close;
			}
			completequest 6008;
			callfunc "Job_Change",Job_Soul_Linker;
			callfunc "F_ClearJobVar";	// Limpa todas as variáveis de classe do jogador
			set SOUL_Q,0;
			mes "[Maia]";
			mes "Desejo a você boa sorte";
			mes "em sua nova vida. Permaneça";
			mes "sempre ao lado de seus aliados, e os";
			mes "espirítos irão te proteger";
			mes "e te ajudar a lutar em suas batalhas. Adeus por agora, amigo.";
			close2;
			set getvariableofnpc(.SoulLinkerTest,"Garoto#link1"),0;
			donpcevent "Timer#link3::OnDisable";
			warp "morocc",157,47;
			end;
		}
		set getvariableofnpc(.SoulLinkerTest,"Garoto#link1"),0;
		mes "[Maia]";
		mes "Hmm...?";
		mes "Parece que o seu tempo";
		mes "de ficar aqui";
		mes "acabou. Vamos voltar";
		mes "para Morroc...";
		close2;
		warp "morocc",157,47;
		end;
	}
	set getvariableofnpc(.SoulLinkerTest,"Garoto#link1"),0;
	if (Class == Job_Soul_Linker) {
		mes "[Maia]";
		mes "Chegou a hora de";
		mes "você seguir seu rumo no";
		mes "mundo! Mais Espiritualistas";
		mes "definitivamente serão necessários nas batalhas contra o mal...";
	}
	else {
		mes "[Maia]";
		mes "Isto é estranho...";
		mes "você não deveria";
		mes "estar aqui. Deixe-me";
		mes "te levar de volta para Morroc...";
	}
	close2;
	warp "morocc",157,47;
	end;
}

job_soul,35,30,6	duplicate(SLTester)	Maia#link6	4_M_KID2

job_soul,30,35,6	script	Espírito do Monge#link4	4_M_GRANDMONK,{
	if (SOUL_Q == 2) {
		mes "[Espírito do Monge]";
		mes "Quem eu sou...?";
		mes "Eu acho...Eu acho";
		mes "melhor você falar";
		mes "com o Maia primeiro...";
		mes "Dizer quem eu sou requer";
		mes "uma explicação complicada...";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Espírito do Monge]";
		mes "Durante minha vida, eu e meus colegas";
		mes "fazíamos o melhor para garantir";
		mes "que faríamos tudo que pudessemos";
		mes "como um Monge. Mesmo assim...";
		mes "Eu ainda não acho que fiz o melhor";
		mes "uso de minhas habilidades.";
		next;
		mes "[Espírito do Monge]";
		mes "Na morte, eu tive muitos arrependimentos,";
		mes "nunca tive a chance de ensinar minhas";
		mes "habilidades para os novos Monges.";
		mes "Emprestar meu poder para os outros";
		mes "é a única chance que tenho de";
		mes "tornar isso possível.";
		next;
		set SOUL_Q,4;
		if (checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		mes "[Espírito do Monge]";
		mes "Eu imploro...";
		mes "Eu preciso de sua ajuda";
		mes "para mostrar a todos";
		mes "meu verdadeiro potencial";
		mes "para os Monges de hoje.";
		close;
	}
	mes "[Espírito do Monge]";
	mes "...";
	close;
}

job_soul,30,25,7	script	Espírito do Sábio#link5	4_M_SAGE_A,{
	if (SOUL_Q == 2) {
		mes "[Espírito do Sábio]";
		mes "Fale com Maia.";
		mes "Tenho medo que eu";
		mes "possa confundir você";
		mes "se você não ouvir primeiro as";
		mes "explicações de Maia...";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Espírito do Sábio]";
		mes "Minha busca pelo conhecimento";
		mes "concedeu-me um poder incrível.";
		mes "Na vida, eu poderia destruir";
		mes "qualquer coisa que quisese. Poucos Sábios";
		mes "conseguiriam chegar ao meu nível...";
		next;
		mes "[Espírito do Sábio]";
		mes "Eu morri, mas eu não sou capaz de";
		mes "passar para o outro mundo.";
		mes "Eu ainda quero usar minhas habilidades. Eu quero usar meu conhecimento";
		mes "para construir o que eu quiser,";
		mes "e destruir o que for preciso.";
		next;
		mes "[Espírito do Sábio]";
		mes "Para mim é suficiente saber";
		mes "que eu posso dar meu poder a um Sábio";
		mes "que seja digno de recebê-lo.";
		mes "Mas para isso, Eu preciso de";
		mes "sua ajuda. Eu imploro,";
		mes "deixe-me ser seu espirito aliado.";
		set SOUL_Q,4;
		if (checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		next;
		mes "[Espírito do Sábio]";
		mes "Eu acredito que você";
		mes "é o único que pode trazer";
		mes "descanso à minha alma";
		mes "perturbada...";
		close;
	}
	mes "[Espírito do Sábio]";
	mes "...";
	close;
}

job_soul,25,30,5	script	Espírito do Alquimista#link7	4_F_ALCHE,{
	if (SOUL_Q == 2) {
		mes "[Espírito do Alquimista]";
		mes "Oh! Eu realmente quero";
		mes "falar com você, mas o que";
		mes "eu tenho a dizer não fará";
		mes "muito sentido a menos que";
		mes "você fale com Maia primeiro. Mas sim,";
		mes "eu realmente preciso de sua ajuda.";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Espírito do Alquimista]";
		mes "Sem exageros, eu era o";
		mes "mais rápido Alquimista da minha";
		mes "época. Na verdade, eu ainda posso ser o";
		mes "Alquimista mais rápido.";
		mes "Mas por isso eu era arrogante, e";
		mes "acabei me matando em um acidente.";
		next;
		mes "[Espírito do Alquimista]";
		mes "Mas a morte não apagou";
		mes "minhas habilidades. Na verdade, eu ainda";
		mes "melhorei minhas habilidades desde que";
		mes "morri. Mas eu não posso";
		mes "ir para o outro mundo, não antes de";
		mes "re-passar minhas tecnicas...";
		set SOUL_Q,4;
		if (checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		next;
		mes "[Espírito do Alquimista]";
		mes "Eu sou impotente como um espirito,";
		mes "mas se tiver sua ajuda, eu posso";
		mes "ser uma influência para os Alquimistas";
		mes "de hoje e ajudá-los a melhorar";
		mes "as suas habilidades. Eu lhe peço,";
		mes "por favor me dê esta chance...";
		close;
	}
	mes "[Espírito do Alquimista]";
	mes "...";
	close;
}

job_soul,1,5,0	script	Timer#link3	HIDDEN_NPC,{
	end;

OnEnable:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	set getvariableofnpc(.SoulLinkerTest,"Garoto#link1"),0;
	end;

OnTimer60000:
OnTimer120000:
	if (getmapusers("job_soul") == 0) {
		stopnpctimer;
		set getvariableofnpc(.SoulLinkerTest,"Garoto#link1"),0;
	}
	end;

OnTimer180000:
OnTimer181000:
OnTimer182000:
	mapwarp "job_soul","morocc",157,47;
	end;

OnTimer183000:
	mapwarp "job_soul","morocc",157,47;
	set getvariableofnpc(.SoulLinkerTest,"Garoto#link1"),0;
	stopnpctimer;
}

sec_in02,35,153,0	script	Variáveis Espiritualista	4_M_OPERATION,{
	callfunc "F_GM_NPC";
	mes "[Variáveis Espiritualista]";
	mes "Eu posso resetar os NPCs da mudança de classe";
	mes "para Espiritualista se um candidato";
	mes "encontrar problemas durante o";
	mes "final da quest. Por favor,";
	mes "não utilize essa função se ainda existirem jogadores no mapa.";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "[Variáveis Espiritualista]";
		mes "Senha";
		mes "incorreta.";
		close;
	} else {
		mes "[Variáveis Espiritualista]";
		mes "Você gostaria de";
		mes "resetar a Variável Global";
		mes "da Mudança de Classe para Espiritualista?";
		next;
		switch(select("Resetar:Cancelar")) {
		case 1:
			mes "[Variáveis Espiritualista]";
			mes "Os NPCs da mudança";
			mes "de classe para Espiritualista";
			mes "foram resetados.";
			set getvariableofnpc(SOUL_Q,"Garoto#link1"),0;
			close;
		case 2:
			mes "[Variáveis Espiritualista]";
			mes "Você cancelou";
			mes "este comando.";
			close;
		}
	}
}

//============================================================ 
// Old changelog
//============================================================ 
//= A temp Soul Linker Job Changer based on the kRO quest.
//= Quest info from RagnaInfo. Sprites from kRO screenshots
//= 1.0 Optimized and updated [Lupus]
//= 1.1 Fixed NPC names according to iRO [Lupus]
//============================================================