//===== rAthena Script ======================================= 
//= Ninja Job Quest
//===== By: ================================================== 
//= Legionaire, Kisuka
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Ninja Job Change Quest
//===== Additional Comments: ================================= 
//= 1.1 Optimized, updated, checked [Lupus]
//= 1.2 Added Akagi [Playtester]
//= 1.3 Updated dialogs to official ones and optimized [SinSloth]
//= 1.4 Fixed a minor bug [SinSloth]
//= 1.5 Updated to match AEGIS script and Added Quest Log commands. [Kisuka]
//= 1.6 Fixed AEGIS amatsu warp typo. [Kisuka]
//= 1.7 Kagerou/Oboro support (Class -> BaseClass) [Euphy]
//============================================================ 
// traduzido pt-br por Weed0 para SOCERO
// revisado por Haikenz

alberta,30,65,3	script	Akaki	730,{
	if (Class == Job_Novice) {
		if (JobLevel == 10) {
			mes "[Akaki]";
			mes "Humm...";
			mes "Você deve ter vindo aqui";
			mes "sentindo que alguém";
			mes "estava esperando por você.";
			mes "Diga-me, você busca";
			mes "o caminho da paciência?";
			next;
			if(select("Não:Sim") == 1) {
				mes "[Akaki]";
				mes "Entendo.";
				mes "Cada um por si,";
				mes "suponho.";
				mes "Cuide-se.";
				close;
			}
			mes "[Akaki]";
			mes "Muito bem.";
			mes "Então, deixe-me";
			mes "levá-lo àquele local";
			mes "logo ali.";
			close2;
			switch(rand(3)) {
				case 1:
					warp "amatsu",170,229;
					end;
				case 2:
					warp "amatsu",216,188;
					end;
				default:
					warp "amatsu",178,176;
					end;
			}
		} else {
			mes "[Akaki]";
			mes "Hm? Não posso te";
			mes "dar nenhum serviço até";
			mes "que se torne um pouco";
			mes "mais forte...";
			close;
		}
	} else {
		mes "[Akaki]";
		mes "Hmm...";
		mes "Você e eu...";
		mes "Somos bastante parecidos em";
		mes "termos de habilidade de combate.";
		mes "Talvez possamos treinar";
		mes "juntos algum dia.";
		close;
	}
}

que_ng,30,65,3	script	Cougar#nq	730,{
	if (Upper == 2) {
		mes "[Cougar]";
		mes "Eu... Eu nunca";
		mes "vi uma criancinha tão";
		mes "poderosa quanto você!";
		mes "S-se afaste, sua";
		mes "aberração da natureza!";
		close;
	}
	if (Class == Job_Novice) {
		if (JobLevel < 10) {
			mes "[Cougar]";
			mes "Como tem passado?";
			mes "Treine duro: você quer ser";
			mes "capaz de desaparecer sem";
			mes "deixar rastros. Se você";
			mes "puder fazer isso, terá o";
			mes "respeito de qualquer Ninja.";
			close;
		}
		if (NINJ_Q == 0) {
			mes "["+strcharinfo(0)+"]";
			mes "Com licença.";
			mes "O-olá?";
			next;
			mes "[Cougar]";
			mes "...............................";
			mes "Como fez isso?";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Fiz o quê?";
			mes "Eu não fiz nada, eu acho que não...";
			next;
			mes "[Cougar]";
			mes "C-como você é capaz de me vez?";
			mes "Era suposto que eu estivesse invisível a olho nu!";
			mes "Ah, agora entendi.";
			mes "Wildcat Joe deve ter te mandado para me matar!";
			mes "Não cairei em seus truques! Morra!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "E-espere! Eu nem mesmo";
			mes " sei quem Wildcat Joe é!";
			mes "Acalme-se, não há";
			mes "motivo para ficar violento!";
			next;
			mes "[Cougar]";
			mes "Quê...?";
			mes "Como se esquivou de todos";
			mes "os meus ataques?";
			mes "Você tem algum talento,";
			mes "eu admito.";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "...............................";
			mes "Eu vim aqui esperando";
			mes "mudar minha classe";
			mes "para Ninja.";
			next;
			mes "[Cougar]";
			mes "...Oh. Isso é tudo?";
			mes "Hmm, você tem grande";
			mes "potencial, mas não posso te ajudar agora.";
			mes "Tenho inimigos demais,";
			mes "e não posso baixar a guarda";
			mes "nem por um segundo.";
			next;
			mes "[Cougar]";
			mes "Aquele Wildcat Joe é";
			mes "completamente implacável...!";
			mes "Ele poderia atacar a qualquer hora!";
			mes "Ele faria qualquer coisa para alcançar";
			mes "a vitória sobre seus inimigos!";
			next;
			mes "[Cougar]";
			mes "Espere, espere, acabei de";
			mes "pensar em algo.";
			mes "Talvez você possa me ajudar.";
			mes "Faça o que peço, e irei ensiná-lo";
			mes "algumas de minhas habilidades se";
			mes "você realmente quiser se tornar um Ninja.";
			next;
			if(select("Com certeza!:Não, obrigado.") == 2) {
				mes "[Cougar]";
				mes "Hm? Bem, tudo bem.";
				mes "Entretanto, não entendo";
				mes "porque não podemos";
				mes "nos ajudar neste pequeno";
				mes "dilema...";
				close;
			}
			mes "[Cougar]";
			mes "Ótimo!";
			mes "Agora, eu queria perguntar ao";
			mes "Wildcat Joe se ele concordaria";
			mes "com uma trégua temporária.";
			mes "Estou ciente que ambos estamos sem armas,";
			mes "então deveríamos nos equipar melhor antes.";
			next;
			mes "[Cougar]";
			mes "Por favor, pegue esta carta";
			mes "e entregue-a a Wildcat Joe,";
			mes "em Einbroch.";
			mes "Ele é um mestre dos disfarces,";
			mes "então mantenha um olhar atento a ele.";
			mes "Ah, e procure-o em um local alto.";
			next;
			mes "[Cougar]";
			mes "Sim, Wildcat Joe";
			mes "sempre teve uma queda";
			mes "por se esconder em lugares altos.";
			mes "De qualquer maneira, depois de";
			mes "entregar-lhe a carta, volte";
			mes "e me diga sua resposta.";
			set NINJ_Q,1;
			setquest 6015;
			close;
		}
		else if (NINJ_Q == 1) {
			mes "[Cougar]";
			mes "Mesmo que esta tarefa";
			mes "não seja tão urgente.";
			mes "Por favor, corra para";
			mes "Einbroch e entregue minha";
			mes "carta para Wildcat Joe.";
			close;
		}
		else if (NINJ_Q == 2) {
			mes "[Cougar]";
			mes "Você entregou aquela";
			mes "carta para Wildcat Joe?";
			mes "Eu ainda preciso saber a resposta dele";
			mes "para minha proposta de trégua.";
			mes "De qualquer maneira,";
			mes "veja se consegue forçá-lo a isso.";
			close;
		}
		else if (NINJ_Q == 3) {
			mes "[Cougar]";
			mes "Ah, você voltou.";
			mes "Então, Wildcat Joe mandou";
			mes "você de volta com a resposta dele?";
			mes "Ótimo, ótimo, por favor,";
			mes "leia-a para mim.";
			next;
			mes "[Cougar]";
			mes "Quê...?!";
			mes "Como ele poderia rejeitar";
			mes "minha proposta de trégua?!";
			mes "Isto só pode significar que";
			mes "ele fez outra Kunai.";
			mes "Poxa! Tenho que alcançá-lo ou estarei arruinado!";
			next;
			mes "[Cougar]";
			mes "Ouça, você tem que";
			mes "me ajudar de novo!";
			mes "Preciso que você me traga alguns materiais";
			mes "para que eu possa criar minha própria Kunai para lutar com Wildcat Joe.";
			mes "Então, cumprirei nosso trato e mudarei sua classe para Ninja.";
			next;
			mes "[Cougar]";
			mes "Tudo o que precisa";
			mes "me trazer é";
			mes "^3355FF5 Cyfars^000000 e";
			mes "^3355FF1 Fracon^000000.";
			mes "Por favor, consiga-os";
			mes "o mais rápido que puder!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Huh?";
			mes "Que engraçado, Wildcat Joe";
			mes "me pediu para conseguir os";
			mes "mesmos materiais.";
			next;
			mes "[Cougar]";
			mes "Maldição! Então isso significa...";
			mes "Que você ajudou Joe em criar sua Kunai!";
			mes "Não!";
			mes "Eu deveria ter pensado nisso antes!";
			mes "Bem, é tarde demais agora.";
			mes "Apenas apresse-se!";
			set NINJ_Q,4;
			changequest 6017,6018;
			close;
		}
		else if (NINJ_Q == 4) {
			if (countitem(7053) < 5 || countitem(1010) < 1) {
				mes "[Cougar]";
				mes "Se apresse e traga";
				mes "^3355FF5 Cyfars^000000 e";
				mes "^3355FF1 Fracon^000000";
				mes "para mim, então poderei";
				mes "criar minha própria Kunai para usar";
				mes "contra Wildcat Joe!";
				close;
			}
			if (SkillPoint != 0) {
				mes "[Cougar]";
				mes "Whoa, whoa...";
				mes "Você ainda tem alguns";
				mes "Pontos de Habilidade restantes.";
				mes "É melhor gastar todos eles";
				mes "antes de mudar de classe,";
				mes "certo?";
				close;
			}
			mes "[Cougar]";
			mes "Ah, você está de volta";
			mes "com tudo que preciso.";
			mes "Voltou antes do que eu";
			mes "esperava, eh?";
			mes "Ótimo, como prometido,";
			mes "te transformarei em um Ninja.";
			next;
			mes "[Cougar]";
			mes "Permita que eu me apresente formalmente.";
			mes "Eu sou o Mestre Ninja Cougar";
			mes "do Grupo Ninja Touga, e estou encarregado";
			mes "do grupo de busca para encontrar";
			mes "Senhor Kazma.";
			next;
			mes "[Cougar]";
			mes "Senhor Kazma é o chefe";
			mes "do meu vilarejo, mas ele fugiu.";
			mes "Isto resultou em um conflito";
			mes "interno entre o Grupo Ninja.";
			mes "As coisas estão bem";
			mes "instáveis agora...";
			next;
			mes "[Cougar]";
			mes "Inicialmente, não queria";
			mes "te aceitar como um Ninja, por";
			mes "causa dessa situação complicada.";
			mes "Contudo, você provou que é";
			mes "realmente digno de se juntar ao";
			mes "grupo dos Ninjas.";
			next;
			mes "[Cougar]";
			mes "De acordo com a carta dele, até";
			mes "mesmo Joe reconhece seu valor. Basta";
			mes "lembrar que, como um Ninja, sua missão";
			mes "é sua maior prioridade. Mas não deixe os objetivos da missão";
			mes "tornem-se mais importantes que sua consciência.";
			next;
			mes "[Cougar]";
			mes "''Sigilo acima de tudo.''";
			mes "Para manter nossos segredos nas sombras,";
			mes "você pode somente comprar ou vender";
			mes "armas Ninja em comerciantes autorizados.";
			mes "Por favor,";
			mes "lembre-se disso.";
			next;
			mes "[Cougar]";
			mes "A partir de hoje, você é";
			mes "um membro do esplêndido Grupo Ninja Touga.";
			mes "Seja tão ágil como o vento,";
			mes "e tão silencioso como a o";
			mes "cair das sombras.";
			callfunc "Job_Change",Job_Ninja;
			set NINJ_Q,5;
			delitem 7053,5; // Cyfar
			delitem 1010,1; // Phracon
			getitem 13010,1;	// Asura
			completequest 6018;
			close;
		}
		else {
			mes "[Cougar]";
			mes "Como tem passado?";
			mes "Treine duro: Você quer";
			mes "ser capaz de desaparecer";
			mes "sem deixar rastros.";
			mes "Se você puder fazer isso,";
			mes "terá o respeito de qualquer Ninja.";
			close;
		}
	} else {
		if (BaseClass == Job_Ninja) {
			mes "[Cougar]";
			mes "Como tem passado?";
			mes "Treine duro: Você quer";
			mes "ser capaz de desaparecer";
			mes "sem deixar rastros.";
			mes "Se você puder fazer isso,";
			mes "terá o respeito de qualquer Ninja.";
			close;
		} else {
			mes "[Cougar]";
			mes "Quê...?";
			mes "Como você foi capaz de me";
			mes "encontrar nas sombras?!";
			mes "Você deve ser mais";
			mes "que um aventureiro comum,";
			mes "eh?";
			close;
		}
	}
}

einbroch,184,194,3	script	Homem Suspeito#nq	 	881,{
	if (NINJ_Q == 1) {
		mes "[Homem Suspeito]";
		mes "Eu viajei para muitos";
		mes "países, mas nunca estive em";
		mes "um prédio tão alto como";
		mes "a Torre de Einbroch.";
		mes "Todos os prédios na minha cidade";
		mes "natal são minúsculos em comparação...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Oh, você é de";
		mes "Amatsu? Estou procurando";
		mes "por alguém chamado";
		mes "Wildcat Joe de lá.";
		next;
		mes "[Homem Suspeito]";
		mes "Não... Não, na verdade";
		mes "sou de Izlude, e estou aqui";
		mes "em Einbroch somente por alguns minérios.";
		mes "Diga-me, por que está";
		mes "procurando por Wildcat Joe?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Bem, preciso entregar";
		mes "esta carta para ele e";
		mes "obter sua resposta para";
		mes "que eu possa me tornar um Ninja.";
		next;
		mes "[Homem Suspeito]";
		mes "Sério?";
		mes "Pensando bem, eu acho que já";
		mes "o encontrei uma ou duas vezes nesta cidade.";
		mes "No entanto, ele prefere ser chamado de ''Red Leopard Joe''";
		mes "ao invés de ''Wildcat Joe.''";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Eu realmente quero te ajudar";
		mes "a encontrá-lo, mas primeiro eu preciso";
		mes "encontrar alguns minérios";
		mes "que estou procurando.";
		mes "Se não se importar, poderia me ajudar?";
		mes "Depois posso te ajudar a encontrar Joe.";
		next;
		if(select("Não se preocupe, eu o encontrarei sozinho.:Claro, eu te ajudarei.") == 1) {
			mes "[Homem Suspeito]";
			mes "Tem certeza...?";
			mes "Red Leopard Joe é realmente";
			mes "um mestre dos disfarces.";
			mes "Você precisará de toda ajudar";
			mes "que puder para encontrá-lo...";
			close;
		}
		mes "[Homem Suspeito]";
		mes "Ótimo, estou feliz por ouvir isso.";
		mes "Por favor, ajude-me";
		mes "a encontrar";
		mes "^3355FF5 Cyfars^000000 e";
		mes "^3355FF1 Fracon^000000.";
		set NINJ_Q,2;
		changequest 6015,6016;
		close;
	}
	else if (NINJ_Q == 2) {
		if (countitem(7053) < 5 || countitem(1010) < 1) {
			mes "[Homem Suspeito]";
			mes "Por favor, traga ";
			mes "^3355FF5 Cyfars^000000 e";
			mes "^3355FF1 Fracon^000000";
			mes "para mim o mais rápido que puder.";
			mes "Então, eu posso te ajudar";
			mes "a encontrar Red Leopard Joe.";
			close;
		}
		mes "[Homem Suspeito]";
		mes "Bom, muito bom.";
		mes "Você trouxe os minerais...";
		mes "Agora, é minha vez de te ajudar.";
		mes "Aqui, deixe-me ver";
		mes "essa carta.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "?????!!";
		next;
		mes "[Homem Suspeito]";
		mes "Por quê? Você não trouxe essa carta de Cougar para mim?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Você é..";
		mes "Você é Wildcat Joe?";
		next;
		mes "[Homem Suspeito]";
		mes "...Sim, mas prefiro ser chamado Red Leopard Joe.";
		mes "Cougar o mandou a mim, certo?";
		mes "Ele é o único que me chama assim.";
		mes "Então você quer ser um ninja, eh?";
		mes "Hmm, está bem.";
		next;
		mes "[Red Leopard Joe]";
		mes "Se você quiser ser um Ninja,";
		mes "deve sempre ser cuidadoso com";
		mes "o que vê e em o quem confia.";
		mes "Não se esqueça de que, se os seus segredos forem descobertos, então você está acabado como Ninja.";
		next;
		mes "[Red Leopard Joe]";
		mes "Lembre-se de se mover";
		mes "rapidamente, e de sempre";
		mes "desaparecer sem deixar";
		mes "rastros. Permanecer";
		mes "oculto nas sombras";
		mes "é realmente nosso";
		mes "poder supremo.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Entendo...";
		mes "...........";
		next;
		mes "[Red Leopard Joe]";
		mes "Agora, deixe-me ler esta carta.";
		mes "Vamos ver... Hm.";
		mes "Pensei que Cougar queria me";
		mes "desafiar de novo, mas na verdade ele";
		mes "quer uma trégua temporária? Hah!";
		next;
		mes "[Red Leopard Joe]";
		mes "Obrigado por sua ajuda,";
		mes "agora que tenho minérios,";
		mes "preciso construir uma Kunai!";
		mes "Hahaha! Não concordarei com uma trégua quando eu tenho a vantagem!";
		next;
		mes "[Red Leopard Joe]";
		mes "De qualquer maneira, deixe-me";
		mes "escrever minha resposta para ele.";
		mes "Também vou escrever uma";
		mes "recomendação para você...";
		mes "Acho que você vai se tornar";
		mes "um excelente Ninja, mesmo";
		mes "que eu tenha acabado de te enganar.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "......";
		mes ".........";
		mes "............";
		next;
		mes "[Red Leopard Joe]";
		mes "Aqui está.";
		mes "Por favor, leve";
		mes "esta carta para Cougar.";
		mes "Demoraria um pouco para";
		mes "voltar para Amatsu, então";
		mes "deixe-me mandá-lo diretamente para lá...";
		delitem 1010,1;	// Phracon
		delitem 7053,5;	// Cyfar
		set NINJ_Q,3;
		changequest 6016,6017;
		close2;
		warp "amatsu",113,127;
		end;
	}
	else if (NINJ_Q == 3) {
		mes "[Red Leopard Joe]";
		mes "Eh?";
		mes "Não sei o que aconteceu, mas";
		mes "parece que você ainda não entregou";
		mes "minha resposta para Cougar.";
		mes "Devo enviar-lhe diretamente";
		mes "para Amatsu agora?";
		next;
		if(select("Não, obrigado.:Sim, por favor.") == 1) {
			mes "[Red Leopard Joe]";
			mes "Certo. Bem, eu estava só";
			mes "tentando poupar algum";
			mes "tempo seu.";
			close;
		}
		mes "[Red Leopard Joe]";
		mes "Okay, então.";
		mes "Adeus por enquanto.";
		close2;
		warp "amatsu",113,127;
		end;
	}
	else if (NINJ_Q == 4) {
		mes "[Red Leopard Joe]";
		mes "Cougar te pediu para";
		mes "obter alguns materiais também?";
		mes "Oh, bem, suponho que";
		mes "não posso culpá-lo.";
		mes "Aliás, devo ser capaz de";
		mes "vencê-lo em uma luta justa.";
		close;
	}
	else if (NINJ_Q == 5 && BaseClass == Job_Ninja) {
		mes "[Red Leopard Joe]";
		mes "Oh, você é um Ninja.";
		mes "Espero que continue a";
		mes "treinar e maximizar todas as";
		mes "habilidades de Ninja que puder.";
		mes "Sempre se lembre de";
		mes "misturar-se com as sombras.";
		close;
	}
	else {
		mes "[Turista]";
		mes "Eu viajei para muitos";
		mes "países, mas nunca estive em um";
		mes "prédio tão alto como a Torre";
		mes "de Einbroch.";
		mes "Todos os prédios na minha";
		mes "cidade natal são minúsculos em comparação...";
		close;
	}
}