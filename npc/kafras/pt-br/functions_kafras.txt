//===== rAthena Script =======================================
//= Kafra Functions
//===== By: ==================================================
//= rAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: =====================================
//= 6.8
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: =================================
//= 1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//=     Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Louyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Louyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=     to block Kafra Password, read comments at 350 line
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=     Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress.
//=     Need all coords for all guides.. and somehow pass their coords to kafra.
//=     Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=     Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=     Updated some functions to handle Guild Kafras the way they should.
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=     Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=     Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//= 6.2 Updated/Fixed warp cords. [Kisuka]
//= 6.3 #kafra_code is now stored as is. [brianluau]
//= 6.4 Removed unofficial Guide function. [Daegaladh]
//= 6.5 Optimized. [Joseph]
//= 6.6 Added Warp Points / View Points Function. [Joseph]
//= 6.7 Added Rune Knight Kafra. [Euphy]
//= 6.8 Updated some functions and added VIP features. [Euphy]
//============================================================
// traduzido e revisado por Haikenz

//=== Main Function ========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	.@welcome = getarg(0);
	.@menu_num = getarg(1);

	// Display Kafra Welcome Message
	switch(.@welcome) {
	default:// Default message (obsolete)
		mes "[Funcionária da Kafra]";
		mes "Bem vindo a";
		mes "Corporação Kafra.";
		mes "Os serviços Kafra";
		mes "sempre estarão ao seu lado.";
		mes "Como posso lhe ajudar?";
		break;
	case 1:// Niflheim specific message
		mes "[Funcionária da Kafra]";
		mes "^666666B-Be-Beeeemmm V-vindooo";
		mes "a c-cooorpoor-r-raaçãããoo";
		mes "K-k-Kafraaaa...^000000";
		break;
	case 2:// Guild Castle Kafra message
		mes "[Funcionária da Kafra]";
		mes "Bem vindo membro do clã. ^ff0000" + getguildname( getcharid(2) ) + "^000000.";
		mes "A Corporação Kafra, está aqui para atender Você.";
		break;
	case 3:// Amatsu specific message (obsolete)
		mes "[Funcionária da Kafra]";
		mes "Entao, Você vem de uma terra distante para estudar nossa cultura, ou Você só está observando?";
		mes "Em ambos os casos, porque não permanecer por algum tempo?";
		mes "Com a eterna fragrância";
		mes "do aroma de flores silvestres.";
		break;
	case 4:// Louyang and Ayothaya specific message (obsolete)
		mes "[Funcionária da Kafra]";
		mes "Com tantas Kafras";
		mes "espalhadas, Você nunca";
		mes "estará longe de casa.";
		break;
	case 5:// NPC has it's own welcome message. (Display nothing)
		break;
	}
	next;
	if (.@welcome == 2)// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
		setarray .@K_Menu0$[0], "Usar Armazém", "Usar Armazém do Clã", "Alugar Carrinho", "Usar Serviço de Teleporte", "Cancelar";
	else {
		switch(.@menu_num) {
			// Save and Storage only
			case 1:	setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Cancelar"; break;
			// Storage only
			case 2:	setarray .@K_Menu0$[0],"Usar Armazém","Cancelar"; break;
			// No Teleport (Common)
			case 3: setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Alugar Carrinho","Checar outra informação","Cancelar"; break;
			// Case 4 is Einbroch no tele message.
			// No save, or teleport. (Common)
			case 5:	setarray .@K_Menu0$[0],"Usar Armazém","Alugar Carrinho","Checar outra informação","Cancelar"; break;
			// Storage and Check Other Information only.
			case 6:	setarray .@K_Menu0$[0],"Usar Armazém","Checar outra informação","Cancelar"; break;
			// Save, Storage, and Pushcart only (Kafra Warehouse)
			case 7:	setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Alugar Carrinho","Cancelar"; break;
			// Save, Storage, Other Check information. (Turbo track)
			case 8: setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Checar outra informação","Cancelar"; break;
			// No Save (Rune Knight)
			case 9: setarray .@K_Menu0$[0],"Usar Armazém","Alugar Carrinho","Usar Serviço de Teleporte","Checar outra informação","Cancelar"; break;
			// Storage, Save, and Pushcart (Dewata, reorder of case 7)
			case 10: setarray .@K_Menu0$[0],"Usar Armazém","Salvar","Alugar Carrinho","Cancelar"; break;
			// Default message (obsolete)
			default: setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Usar Serviço de Teleporte","Alugar Carrinho","Checar outra informação","Cancelar"; break;
		}
	}
	.@menu$ = implode(.@K_Menu0$,":");
	while (true) {
		.@j = select(.@menu$) - 1;
		if (.@K_Menu0$[.@j] == "Salvar")
			return;
		else if (.@K_Menu0$[.@j] == "Usar Armazém") {
			if (.@welcome == 2)// Do not charge for Guild Storage
				callfunc "F_KafStor",2,0,0;
			else
				callfunc "F_KafStor",0,getarg(3),.@welcome;
			next;
		}
		else if (.@K_Menu0$[.@j] == "Usar Serviço de Teleporte") {
			if (.@menu_num != 4)// Display Einbroch "No Teleport Service" notice.
				callfunc "F_KafTele",.@welcome;
			else {
				mes "[Funcionária da Kafra]";
				mes "Por causa do ^FF0000Acordo de limite";
				mes "de transporte^000000, a";
				mes "Corporação Kafra não pode";
				mes "fornecer serviços de teletransporte";
				mes "aqui na república de Schwartzwald.";
				next;
				mes "[Funcionária da Kafra]";
				mes "Nós solicitamos a Você utilizar";
				mes "o serviço de Aeroplano no lugar";
				mes "do teletransporte. Muito obrigado por sua";
				mes "compreensão e cooperação.";
			}
			next;
		}
		else if (.@K_Menu0$[.@j] == "Alugar Carrinho") {
			if (callfunc("F_KafCart",.@welcome,getarg(4)) == 1)
				next;
		}
		else if (.@K_Menu0$[.@j] == "Checar outra informação") {
			callfunc "F_KafInfo",getarg(2);
			next;
		}
		else if (.@K_Menu0$[.@j] == "Cancelar") {
			callfunc "F_KafEnd",.@welcome,0;
			end;
		}
		else if (.@K_Menu0$[.@j] == "Usar Armazém do Clã") {
			callfunc "F_KafStor",1,0;
			next;
		}
	}
}

//===================================================
// Storage Function
//===================================================
function	script	F_KafStor	{
	.@type = getarg(0);
	.@fee = getarg(1);

	// Unable to access Guild Storage (Busy)
	if (.@type == 1) {
		if (guildopenstorage()) {
			mes "[Funcionária da Kafra]";
			mes "Sinto muito, mas outro membro do Cla está usando o armazém neste instante.";
			mes "Por favor, aguarde até que esta pessoa termine.";
			close3;
		}
		cutin "", 255;
		close;
	}
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if (!callfunc("F_CanOpenStorage")) {
		mes "[Funcionária da Kafra]";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666S-D-ddeessssculllpppe,";
			mes "v-vo-voooccee éee umm";
			mes "Appreenddizz... P-Prreeecisa teerrr";
			mes "nívveelll d-de hh-habibilidaddeee nível 6...^000000";
			return;
		}
		mes "Lamento, mas Você";
		mes "precisa ser um Aprendiz com ";
		mes "nível mínimo de habilidade";
		mes "level 6 para usar o armazém.";
		return;
	}
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if (.@type != 2) {
		// Consume "Free Ticket for Kafra Storage" if available.
		if (countitem(7059))
			delitem 7059,1;
		else {
			if (Zeny < .@fee) {
				mes "[Funcionária da Kafra]";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeeny...";
					mes "P-Preeciso de Z-Zeny...!";
					mes "P-Preciiiso de 150... Zeny...";
					mes "Ergh! T-tomando e-energia!^000000";
					return;
				}
				// Standard Message
				mes "Lamento, mas Você não possui";
				mes "Zeny suficientes para usar";
				mes "o serviço de armazém.";
				mes "Para acessar o armazém o custo é: " + .@fee + " Zeny.";
				return;
			}
			Zeny -= .@fee;
			RESRVPTS += (.@fee/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (.@type != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[Funcionária da Kafra]";
			for ( .@i = 0; .@i < 5; .@i++ )
				mes "^666666Obrigado.. por... usar...^000000";
		}
		// Normal message
		else {
			mes "[Funcionária da Kafra]";
			mes "Aqui, deixe-me abrir";
			mes "seu Armazém para Você.";
			mes "Obrigado por usar";
			mes "o Serviço Kafra.";
		}
	}
	callfunc("F_CheckKafCode");	//check your storage password, if set
	close2;
	openstorage;
	cutin "", 255;
	end;
}

//===================================================
// Teleport Function
//===================================================
function	script	F_KafTele	{
	mes "[Funcionária da Kafra]";
	mes "Por favor, escolha seu destino.";
	next;
	.@j = select( implode(@wrpC$,":") ) - 1;
	if (@wrpC$[.@j] == "Cancelar")
		return;
	// Consume "Free Ticket for Kafra Transportation" if available.
	// Do not consume if in Guild castle
	if (countitem(7060) > 0 && getarg(0) != 2)
		delitem 7060,1;
	else {
		if (Zeny < @wrpP[.@j]) {
			mes "[Funcionária da Kafra]";
			mes "Sinto muito, mas Você não possui";
			mes "Zeny para teleportar.";
			mes "O serviço de teleporte ";
			mes "para " + @wrpD$[.@j] + " é " + @wrpP[.@j] + " Zeny.";
			close3;
		}
		Zeny -= @wrpP[.@j];
		RESRVPTS += (@wrpP[.@j]/16);
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "Al De Baran") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "Alberta") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "Comodo") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "Comodo Ilha do Farol") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "Geffen") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "Izlude") {
		if (checkre(RENEWAL) == true)
			warp "izlude", 128, 98;
		else
			warp "izlude", 91, 105;
	}
	else if (@wrpD$[.@j] == "Juno") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "Mina Abandonada de Mjolnir") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "Morroc") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "Caverna dos Orcs") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "Payon") warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "Prontera") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "Umbala") warp "umbala", 100, 154;
	end;
}


//===================================================
// Cart Function
//===================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if (BaseClass != Job_Merchant) {
		mes "[Funcionária da Kafra]";
		mes "Me desculpe, mas o";
		mes "serviço de aluguel de carrinho";
		if (checkre(RENEWAL) == true)
			mes "está disponível apenas para Mercadores.";
		else {
			mes "está disponível para Mercadores,";
			mes "Ferreiros, Mestre Ferreiros,";
			mes "Alquimistas e Bioquímicos.";
		}
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	else if (checkcart() == true) {
		mes "[Funcionária da Kafra]";
		mes "Você já possui";
		mes "um carrinho equipado.";
		mes "Infelizmente, nós não podemos";
		mes "alugar mais de um";
		mes "carrinho por vez.";
		return 1;
	}
	// Check if the player has the Pushcart skill
	else if (getskilllv("MC_PUSHCART") == 0) {
		mes "[Funcionária da Kafra]";
		mes "Você só pode alugar um carrinho depois que aprender a habilidade \"Carrinho\" .";
		return 1;
	}
	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if (countitem(7061) > 0 && getarg(0) != 2)
		delitem 7061,1;
	else {
		.@rental_fee = getarg(1);
		mes "[Funcionária da Kafra]";
		mes "O aluguel do carrinho de mão";
		mes "custará " + .@rental_fee + " zeny.";
		mes "Você deseja alugar um carrinho de mão?";
		next;
		if (select("Alugar Carrinho.:Cancelar") == 2)
			return 0;
		if (Zeny < .@rental_fee) {
			mes "[Funcionária da Kafra]";
			mes "Lamento, mas Você";
			mes "não possui Zeny";
			mes "suficientes para alugar um carrinho";
			mes "o aluguel custa " + .@rental_fee + " Zeny.";
			return 1;
		}
		Zeny -= .@rental_fee;
		RESRVPTS += 48;
	}
	setcart;
	return 1;
}

//===================================================
// Funçao especial de pontos de reserva
//===================================================
function	script	F_KafInfo	{
// Descomente a próxima linha para bloquear o Kafra Storage Protection
//	.@block = 1;
	if (getarg(0) == 2)// Porto Malaya, Cavaleiro Rúnico
		setarray .@menu$[0], "Checar Pontos.", "", "", "Cancelar";
	else if (.@block)
		setarray .@menu$[0], "Checar Pontos.", "", "Localização das Kafras", "Cancelar";
	else
		setarray .@menu$[0], "Checar Pontos.", "Serviço de Senha do Armazém", "Localização das Kafras", "Cancelar";
	.@menu_list$ = implode(.@menu$,":");
	while (true) {
		.@j = select(.@menu_list$) - 1;
		switch(.@j) {
		case 0:// Verifica os pontos especiais de reserva.
			mes "[Funcionária da Kafra]";
			mes "Vamos ver...";
			mes "" + strcharinfo(0) + "...";
			mes "Ah, Você tem um total de";
			mes "" + RESRVPTS + " Pontos.";
			next;
			mes "[Funcionária da Kafra]";
			mes "Você pode trocar seus";
			mes "Pontos Especiais de Reserva em";
			mes "recompensas no escritório principal de Kafra em Al De Baran. Por favor use nossos";
			mes "serviços para ter mais benefícios do nosso programa de recompensas.";
			if (getarg(0) == 1)
				return;
			next;
			break;
		case 1:// Serviço de senha de armazenamento
			callfunc("F_SetKafCode","[Funcionária da Kafra]","Serviços Kafra");
		case 2:// Localizaçao das Funcionárias da Kafra
			for ( .@i = 0; .@i <= 3; .@i++ )
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			next;
			for ( .@i = 0; .@i <= 3; .@i++ )
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			break;
		default:
			return;
		}
	}
}

// === End Function ==================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	.@type = getarg(0);
	mes "[Funcionária da Kafra]";
	if (getarg(1) == true) {// Save
		mes "Seu ponto de renascimento";
		mes "foi salvo aqui.";
		mes "" + getarg(2) + ".";
		mes "Muito obrigado por usar";
		mes "os serviços da Corporação Kafra.";
	} else if (.@type == 0 || .@type == 5) {// Generic End
		mes "Nós da Corporação Kafra,";
		mes "sempre estamos nos esforçando para proporcionar os melhores serviços. Esperamos satisfazer as suas necessidades de aventureiro nos seus padrões de excelência.";
	} else if (.@type == 1) {// Niflheim End
		percentheal 0,-25;
		mes "^666666Kaffffra n-nunca";
		mes "mooooorrrrre. Semprre...";
		mes "Es-tá ao seeeuuu laaadooo~^000000";
	} else if (.@type == 2) {// RockRidge Save
		mes "foi salvo aqui.";
		mes "Muito obrigado por usar";
		mes "os serviços da Corporação Kafra.";
	}
	close3;
}

//===================================================
// Check Storage Password Function
//===================================================
function	script	F_CheckKafCode	{
	if (!#kafra_code)
		return;
	mes "Digite a senha do armazém:";
	input .@code_;
	if (.@code_ != #kafra_code) {
		dispbottom "Senha incorreta.";
		close3;
	}
	@kafcode_try = 0;
	return;
}

// === Set / Change / Clear Storage Password Function ===
// getarg(0): NPC Name
// getarg(1): Company Name
//=======================================================
function	script	F_SetKafCode	{
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	if (#kafra_code == 0) {
		mes "" + .@comp_name$ + " orgulhosamente apresenta um novo serviço:";
		mes "Proteção adicional do seu armazém com senha.";
		next;
		if (select( "Definir nova senha -> 5000z", "Cancelar" ) == 1)
			callsub( S_SET, .@npc_name$, .@comp_name$ );
		close3;
	}
	mes "Seu armazém está protegido com uma senha. O que Você fará agora?";
	next;
	switch( select( "Mudar senha antiga -> 5000z", "Remover senha do Armazém -> 1000z", "Cancelar" ) ) {
	case 1:
		mes .@npc_name$;
		mes "Primeiro, digite sua ^0000FFsenha antiga^000000.";
		.@code = callfunc("F_EntKafCode");
		if (!.@code || .@code != #kafra_code) {
			mes "Senha errada. Você não pode inserir uma nova senha.";
			emotion ET_SCRATCH;
			break;
		}
		next;
		callsub( S_SET, .@npc_name$, .@comp_name$ );
		break;
	case 2:
		mes .@npc_name$;
		mes "Por favor, entre com a senha antes de sua retirada.";
		.@code = callfunc("F_EntKafCode");
		if (!.@code) {
			mes "A senha não foi removida.";
			emotion ET_SCRATCH;
			break;
		}
		next;
		mes .@npc_name$;
		if (Zeny < 1000) {
			mes "Você não possui Zeny.";
			emotion ET_MONEY;
			break;
		}
		Zeny -= 1000;
		if (.@code == #kafra_code) {
			#kafra_code = 0;
			mes "Você teve exito ao colocar senha em seu armazém.";
			mes "Muito obrigado por usar os " + .@comp_name$ + ".";
			emotion ET_THANKS;
		} else
			{
			mes "Senha errada! não devolverei seus 1000 Zeny.";
			mes "Por favor, na próxima entre com a senha correta.";
			emotion ET_SORRY;
		}
		break;
	case 3:
		break;
	}
	close3;

S_SET:
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	mes "Now enter your ^FF0000nova senha^000000 para proteger seu armazém de roubos.";
	.@code = callfunc("F_EntKafCode");
	if (!.@code) {
		mes "A senha não foi trocada.";
		emotion ET_SCRATCH;
		return;
	}
	next;
	mes .@npc_name$;
	if (Zeny < 5000) {
		mes "Você não possui Zeny suficiente.";
		emotion ET_MONEY;
		return;
	}
	Zeny -= 5000;
	#kafra_code = .@code;
	mes "Seu armazém está protegido com uma senha secreta.";
	mes "Obrigado por usar " + .@comp_name$ + ".";
	emotion ET_THANKS;
	return;
}

//===================================================
// Basic Password Validation Function
//===================================================
function	script	F_EntKafCode	{
	mes "Enter a number 1000~10000000:";
	@kafcode_try += 1;
	if (@kafcode_try > 10) {
		@kafcode_try = 0;
		logmes "Hack: Tentou colocar senha de armazenamento.";
		return 0;
	}
	if (input(.@code_) == 1) {
		mes "Você não pode usar uma senha tao grande.";
		return 0;
	}
	if (.@code_ < 1000) {
		mes "Você não pode usar uma senha tao curta.";
		return 0;
	}
	return .@code_;
}

//===================================================
// Warp Points / View Points Function
//===================================================
function	script	F_KafSet	{
	deletearray @wrpC$[0], getarraysize(@wrpC$);
	deletearray @wrpD$[0], getarraysize(@wrpD$);
	deletearray @wrpP$[0], getarraysize(@wrpP$);
	deletearray @viewpX[0], getarraysize(@viewpX);
	deletearray @viewpY[0], getarraysize(@viewpY);

	.@map$ = strnpcinfo(4);
	if (.@map$ == "prontera") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Caverna dos Orcs", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1700, 1800;
		setarray @viewpX[0], 151, 29, 282, 152;
		setarray @viewpY[0], 29, 207, 200, 326;
	} else if (.@map$ == "alberta") {
		setarray @wrpP[0], 1200, 1800, 1800;
		setarray @wrpD$[0], "Payon", "Morroc", "Prontera";
		setarray @viewpX[0], 28, 113, 0, 0;
		setarray @viewpY[0], 229, 60, 0, 0;
	} else if (.@map$ == "aldebaran") {
		setarray @wrpP[0], 1200, 1200, 1800, 1700;
		setarray @wrpD$[0], "Geffen", "Juno" , "Izlude", "Mina Abandonada de Mjolnir";
	} else if (.@map$ == "comodo" ) {
		setarray @wrpD$[0], "Morroc", "Comodo Ilha do Farol", "Umbala";
		setarray @wrpP[0], 1800, 1200, 1800;
	} else if (.@map$ == "cmd_fild07") {
		setarray @wrpD$[0], "Comodo", "Morroc";
		setarray @wrpP[0], 1200, 1200;
	} else if (.@map$ == "geffen") {
		setarray @wrpD$[0], "Prontera", "Al De Baran", "Caverna dos Orcs", "Mina Abandonada de Mjolnir";
		setarray @wrpP[0], 1200, 1200, 1700, 1700;
		setarray @viewpX[0], 120, 203;
		setarray @viewpY[0], 62, 123;
	} else if (.@map$ == "izlude") {
		setarray @wrpD$[0], "Geffen", "Payon", "Morroc", "Al De Baran";
		setarray @wrpP[0], 1200, 1200, 1200, 1800;
	} else if (.@map$ == "morocc") {
		setarray @wrpD$[0], "Prontera", "Payon", "Alberta", "Comodo", "Comodo Ilha do Farol";
		setarray @wrpP[0], 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX[0], 156, 163, 28, 292;
		setarray @viewpY[0], 97, 260, 167, 211;
	} else if (.@map$ == "umbala") {
		setarray @wrpD$[0], "Comodo";
		setarray @wrpP[0], 1800;
	} else if (.@map$ == "payon") {
		setarray @wrpD$[0], "Prontera", "Alberta", "Morroc";
		setarray @wrpP[0], 1200, 1200, 1200;
	} else if (.@map$ == "yuno") {
		setarray @wrpD$[0], "Al De Baran";
		setarray @wrpP[0], 1200;
		setarray @viewpX[0], 328, 278, 153, 0;
		setarray @viewpY[0], 108, 221, 187, 0;
	} else if (.@map$ == "job3_rune01") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
	}

	.@warp_size = getarraysize(@wrpD$);

	// VIP pricing varies across the regional servers; this is most practical.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		for ( .@i = 0; .@i < .@warp_size; .@i++ )
			@wrpP[.@i] *= 2;
	}

	for ( .@i = 0; .@i < .@warp_size; .@i++ )
		@wrpC$[.@i] = @wrpD$[.@i] + " -> " + @wrpP[.@i] + " z";
	@wrpC$[ .@warp_size ] = "Cancelar";
	return;
}