//===== rAthena Script =======================================
//= Einbroch Airport Staff
//===== By: ==================================================
//= L0ne_W0lf, Muad_Dib
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Einbroch Airport Staff
//===== Additional Comments: =================================
//= 1.0 First version.
//============================================================
// traduzido e revisado por Haikenz

airport,143,43,5	script	Equipe do Aeroporto#airport1a::airport1	90,{
	mes "[Equipe do Aeroporto]";
	mes "Bem-vind"+(Sex?"o":"a")+" ao";
	mes "Aeroporto de Einbroch,";
	mes "onde oferecemos voos frequentes";
	mes "para Juno, Lighthalzen e Hugel.";
	next;
	if (select("Embarcar no dirigível.:Cancelar.") == 1) {
		mes "[Equipe do Aeroporto]";
		mes "A taxa do voo é de";
		mes "1,200 zeny, mas caso você";
		mes "possua o Bilhete para o Aeroplano,";
		mes "essa taxa não é cobrada.";
		next;
		if (select("Sim.:Não.") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Bilhete para o Aeroplano
				warp "airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airport",148,51;
				end;
			}
			mes "[Equipe do Aeroporto]";
			mes "Desculpe, mas você não possui";
			mes "os 1,200 zenys para pagar";
			mes "o seu voo, ou não possui o Bilhete.";
			close;
		}
	}
	mes "[Equipe do Aeroporto]";
	mes "Obrigado e tenha um bom dia.";
	close;
}

airport,158,43,5	duplicate(airport1)	Equipe do Aeroporto#airport1b	90
airport,126,43,5	duplicate(airport1)	Equipe do Aeroporto#airport1c	90

airport,143,49,3	script	Equipe de Recepção#airport2a::airport2	90,{
	mes "[Equipe de Recepção]";
	mes "Bem-vind"+(Sex?"o":"a")+" ao Aeroporto de Einbroch.";
	mes "Deixe-me guiá-lo ao seu terminal principal";
	mes "caso você tenha acabado de chegar.";
	mes "Ou embarque no dirigível em busca de outro destino,";
	mes "sendo eles Juno, Lighthalzen e Hugel.";
	next;
	if (select("Deixar o terminal principal.:Cancelar.") == 1) {
		mes "[Equipe de Recepção]";
		mes "Uma vez no terminal principal, você precisará pagar outra";
		mes "taxa para embarcar no dirigível. você só deverá";
		mes "sair caso a cidade de Lighthalzen for seu último destino.";
		mes "Gostaria de prosseguir ao terminal principal?";
		next;
		if (select("Sim.:Não.") == 1) {
			warp "airport",142,40;
			end;
		}
	}
	mes "[Equipe de Recepção]";
	mes "Certo, obrigado";
	mes "pelo seu apoio e";
	mes "espero que você tenha";
	mes "um voo agradável.";
	close;
}

airport,126,51,3	duplicate(airport2)	Equipe de Recepção#airport2b	90
airport,158,50,3	duplicate(airport2)	Equipe de Recepção#airport2c	90

einbroch,94,267,3	script	Equipe do Aeroporto#ein01	91,{
	mes "[Equipe do Aeroporto]";
	mes "Bem-vind"+(Sex?"o":"a")+" ao";
	mes "Aeroporto de Einbroch.";
	mes "Utilize essa porta para";
	mes "embarcar no digerível que";
	mes "te leva para Juno, Lighthalzen e";
	mes "Hugel na República de Schwarzwald.";
	next;
	mes "[Equipe do Aeroporto]";
	mes "Caso Einbroch seja o seu destino,";
	mes "desça as escadas e fale com";
	mes "nossa Equipe de Recepção";
	mes "para levá-lo para o";
	mes "terminal principal. Obrigado,";
	mes "e tenha um voo agradável.";
	close;
}